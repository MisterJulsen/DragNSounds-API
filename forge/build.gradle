plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "dragnsounds-common.mixins.json"
        mixinConfig "dragnsounds.mixins.json"
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common


    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:$rootProject.forge_version"

    modImplementation(include("de.mrjulsen.mcdragonlib:dragonlib-forge:${rootProject.minecraft_version}-${rootProject.dragonlib_version}"))
    modImplementation("net.minecraftforge:forgeconfigapiport-fabric:${rootProject.forge_config_api_port_version}")
    
    if (rootProject.includeDependencies.toBoolean()) {
        modImplementation(forgeRuntimeLibrary(include("ws.schild:jave-core:3.5.0")))
        modImplementation(forgeRuntimeLibrary(include("ws.schild:jave-nativebin-linux32:3.5.0")))
        modImplementation(forgeRuntimeLibrary(include("ws.schild:jave-nativebin-linux64:3.5.0")))
        modImplementation(forgeRuntimeLibrary(include("ws.schild:jave-nativebin-linux-arm32:3.5.0")))
        modImplementation(forgeRuntimeLibrary(include("ws.schild:jave-nativebin-linux-arm64:3.5.0")))
        modImplementation(forgeRuntimeLibrary(include("ws.schild:jave-nativebin-win32:3.5.0")))
        modImplementation(forgeRuntimeLibrary(include("ws.schild:jave-nativebin-win64:3.5.0")))
        modImplementation(forgeRuntimeLibrary(include("ws.schild:jave-nativebin-osxm1:3.5.0")))
        modImplementation(forgeRuntimeLibrary(include("ws.schild:jave-nativebin-osx64:3.5.0")))
    }

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowBundle]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

